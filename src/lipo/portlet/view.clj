(ns lipo.portlet.view
  "Portlet that simply and optionally edits a content page by id or path.

  Can show other portlets that are defined in the content.

  In content, portlets can be defined using with EDN map surrounded by brackets.


  Example:
  {{:portlet/type :some-portlet :some \"option\"}}

  Will replace that with the rendered portlet."
  (:require [lipo.portlet :as p]
            [lipo.content-db :as content-db]
            [lipo.format :as format]
            [ripley.html :as h]
            [xtdb.api :as xt]
            [ripley.live.source :as source]
            [lipo.content-model :as content-model]
            [cheshire.core :as cheshire]
            [ripley.js :as js]
            [clojure.string :as str]
            [lipo.db :as db]
            [taoensso.timbre :as log]
            [lipo.localization :refer [tr! tr]]
            [ripley.live.collection :as collection]
            [re-html-template.core :refer [html]]
            [lipo.attachments :as attachments])
  (:import (java.util UUID)))


(def editor-config
  {:toolbar
   {:items [:heading
            :|
            :bold
            :italic
            :link
            :bulletedList
            :numberedList
            :|
            :outdent
            :indent
            :|
            :imageUpload
            :blockQuote
            :insertTable
            :mediaEmbed
            :undo
            :redo
            :horizontalLine
	    :findAndReplace
	    :fontBackgroundColor
	    :fontColor
	    :fontFamily
	    :htmlEmbed
            ]}
   :language :fi
   :image {:toolbar [:imageTextAlternative
                     :imageStyle:inline
                     :imageStyle:block
                     :imageStyle:side
                     :linkImage]}
   :table {:contentToolbar [:tableColumn
                            :tableRow
                            :mergeTableCells]}
   :simpleUpload
   {:uploadUrl "/_upload"
    :withCredentials true}
   })

(defn text-field
  [{:keys [name label value required?]}]
  (h/html
    [:div.my-3
     [:label {:class "block" :for name}
      label (h/dyn! (when required?
                      "*"))]
     [:input {:id name :name name :value value
              :required required?}]]))

(defn- editor-form
  "Create an editor form for updating an existing document or creating a new one."
  [{:content/keys [title excerpt body type path] :as content} save! delete! cancel]
  (h/html
   [:div
    [:form.editor {:on-submit ["
    const myContent = tinymce.activeEditor.getContent();
    document.getElementById('newbody').value = myContent"
                               (js/js save! (js/form-values "form.editor")) js/prevent-default]}
     ;; Show path field when creating new sub page
     [::h/when (and (not path) (not= (:xt/id content) "root"))
      (text-field {:name "path"
                   :label (tr [:fields :content/path])
                   :required? true})]

     [:div.my-3
      [:label {:class "block" :for "type"}
       (tr! [:fields :content/type])]
      [:select {:name "type"
                :required true}
       [::h/for [t content-model/content-types
                 :let [value (name t)
                       ;; translate this
                       label (name t)
                       selected? (= t type)]]
        [:option {:value value :selected selected?} label]]]]

     (text-field {:name "title"
                  :label (tr [:fields :content/title])
                  :value title
                  :required? true})
     [:div.my-3
      [:label (tr! [:fields :content/excerpt])]
      [:textarea {:name "excerpt"}
       excerpt]]
     [:input#newbody {:name "body" :type "hidden"}]
     [:div#content-body]
     [:div.flex.justify-between.mt-3
      ;; Show delete only for existing content
      [::h/if path
       [:button.danger
        {:on-click [#(delete! content) js/prevent-default]}
        (tr! [:buttons :delete])]
       [:div]]
      [:div.flex.flex-row
       [:button.secondary.mr-2
        {:on-click [cancel js/prevent-default]}
        (tr! [:buttons :cancel])]
       [:button.primary
        {:type "submit"}
        (tr! [:buttons :save])]]]

     [:script
      (h/out!
       "tinymce.init({
          selector: 'div#content-body',
        });")]]]))

(defn- body-with-portlets
  "Read portlet definitions from body, returns sequence of
  strings and portlet definition maps."
  [body-html-str]
  (if-let [start (str/index-of body-html-str "{{")]
    (let [before (subs body-html-str 0 start)
          after (subs body-html-str (inc start))
          end (str/index-of after "}}")]
      (if end
        (let [definition (subs after 0 (inc end))
              after (subs after (+ 2 end))]
          ;; Found portlet definition, try to read it
          (concat
           (list before
                 (try (binding [*read-eval* false]
                        (-> definition
                            ;; Fix unicode LEFT/RIGHT DOUBLE QUOTATION MARK to
                            ;; regular ascii double quote
                            (str/replace \u201c \")
                            (str/replace \u201d \")
                            read-string))
                      (catch Throwable t
                        (log/debug t "Unparseable portlet definition:" definition)
                        "[ERROR: unparseable portlet definition.]")))
           (body-with-portlets after)))
        ;; No end found, return as is
        (list body-html-str)))

    ;; No portlet definition start found, return as is
    (list body-html-str)))

(defn- render-body-with-portlets [{here :here :as ctx} body]
  (doseq [part (body-with-portlets body)]
    (if (string? part)
      ;; FIXME: body *must* be well formed HTML here
      (h/out! part)
      ;; If part is has type :view with the same path as this
      ;; don't recursively render the same page in an infinite loop
      (when-not (and (= :view (:portlet/type part))
                     (= here (:path part here)))
        (p/render ctx part)))))


(defn- attachments-row [ctx row]
  (html
   {:file "templates/attachments.html" :selector "tbody tr"}

   {:href "download-link"} {:set-attributes
                            {:href (str "/_img?id=" (:xt/id row))}}
   "data-field"
   {:let-attrs {f :data-field}
    :replace-children (some->> f keyword (get row) format/display h/dyn!)}

   :button.delete-attachment
   {:set-attributes {:on-click (js/js #(when %
                                         (attachments/delete! ctx (:xt/id row)))
                                      (str "confirm('" (tr [:attachments :confirm-delete])  "')"))}}))

(defn- attachments
  "Manage attachments added to this page."
  [ctx id]
  (let [attachments (db/q-source (:xtdb ctx)
                                 '{:find [(pull ?f [:file/name :file/size
                                                    :meta/created
                                                    :xt/id]) ?lname]
                                   :where [[?f :file/content ?id]
                                           [?f :file/name ?name]
                                           [(clojure.string/lower-case ?name) ?lname]]
                                   :in [?id]
                                   :order-by [[?lname :asc]]}
                                 id)]
    (html
     {:file "templates/attachments.html" :selector "div.attachments"}
     :tbody {:replace
             (collection/live-collection
              {:key :xt/id
               :container-element :tbody
               :render (partial attachments-row ctx)
               :source (source/c= (mapv first %attachments))})}
     {:name "content-id"} {:set-attributes
                           {:value (str id)}})))

(defn- view-or-edit-content [{:keys [xtdb] :as ctx}
                             path can-edit?
                             set-edit-state!
                             {:keys [editing? sub-page? creating?] :as edit-state}]
  (let [db (xt/db xtdb) ; take fresh db on each render
        id (content-db/content-id db path)
        {:content/keys [title body excerpt]
         :meta/keys [modifier modified
                     creator created] :as content}
        (xt/entity db id)
        created-formatted (format/display created)
        modified-formatted (format/display modified)
        cancel #(set-edit-state! {:editing? false
                                  :creating? false
                                  :sub-page? false
                                  :content content})]
    (h/html
     [:div.content-view.ck-content
      [::h/when (and can-edit? (and
                                 (not editing?)
                                 (not creating?)))
       [:div.flex.justify-end
        [:button.primary.small.mr-3
         {:on-click #(set-edit-state! {:editing? true
                                       :sub-page? false
                                       :content content})}
         (tr! [:buttons :edit])]
        [:button.secondary.small
         {:on-click #(set-edit-state! {:creating? true
                                       :sub-page? true
                                       :content {:xt/id (UUID/randomUUID)
                                                 :content/parent id}})}
         (tr! [:buttons :create-sub-page])]]]
      [::h/if (or editing? creating?)
       [:<>
        (editor-form
         (:content edit-state)
         (partial content-db/save! ctx set-edit-state! sub-page? creating? (:content edit-state))
         (partial content-db/delete! ctx)
         cancel)

        (when-not creating?
          (attachments ctx id))]
       [:<>
        [:h1.my-3 title]
        [:p.mb-3.text-sm.font.text-gray-500
         (tr! [:fields :meta/created]) ": " created-formatted]
        [::h/when modified
         [:p.mb-3.text-sm.font.text-gray-500
          (tr! [:fields :meta/modified]) ": " modified-formatted]]
        [::h/when excerpt
         [:p.excerpt
          excerpt]]

        (when body
          (render-body-with-portlets ctx body))]]])))

(defmethod p/render :view [{:keys [db can-edit?] :as ctx} {:keys [path inline-edit? raw?]}]
  (let [path (or path (:here ctx))]
    (if raw?
      ;; Show raw content (body only)
      (h/html
       [:div.content-view.ck-content
        (h/out! (:content/body (xt/entity db (content-db/content-id db path))))])

      ;; So content with all the bells and whistles and edit UI
      (let [can-edit? (and inline-edit? can-edit?)
            [edit-source set-edit-state!] (source/use-state {:editing? false})]
        (h/html
         [:div
          [::h/live edit-source
           (partial view-or-edit-content ctx path can-edit? set-edit-state!)]])))))
